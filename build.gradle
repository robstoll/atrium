buildscript {
    rootProject.version = '0.8.0-SNAPSHOT'
    rootProject.group = 'ch.tutteli.atrium'

    def translationProjects = subprojects.findAll { it.projectDir.path.contains("translations") }
    ext {
        // main
        kbox_version = '0.12.0'
        kbox = { "ch.tutteli.kbox:kbox:$kbox_version" }
        kotlin_version = '1.2.71'
        mockito_kotlin_version = '1.5.0'
        mockito = { "com.nhaarman:mockito-kotlin:$mockito_kotlin_version" }

        // test
        jacoco_tool_version = '0.8.2'
        junit_platform_version = '1.2.0'
        jupiter_version = '5.3.1'
        spek_version = '1.1.5'

        //gh-pages.gradle
        docProjects = subprojects.findAll { !it.name.contains("robstoll") && it.name != "${rootProject.name}-spec" }
        ghPages_version = rootProject.version
        dokka_sourceMapping = 'tree/master'
        srcKotlin = 'src/main/kotlin'
        github_url = "https://github.com/robstoll/${rootProject.name}"

        //jacoco-multi-project.gradle
        def deprecatedProjects = subprojects.findAll { it.name.endsWith("-deprecated") }
        jacocoMulti = [
            sourceProjects:
                (subprojects - deprecatedProjects).findAll {
                    !it.name.endsWith("-js") &&
                        !it.name.endsWith("-android") &&
                        // would have two classes with the same name if we add it as project as well,
                        // (clashes with "${project.name}-translations-en_GB-jvm"
                        it.name != "${rootProject.name}-translations-de_CH-jvm" &&
                        // does not make sense to listen specs in coverage
                        it.name != "${rootProject.name}-spec"
                },
            jacocoProjects:
                (subprojects - deprecatedProjects - translationProjects).findAll {
                    !it.name.endsWith("-common") &&
                        !it.name.endsWith("-js") &&
                        !it.name.endsWith("-android") &&
                        it.name != "${rootProject.name}-domain-api-jvm" &&
                        it.name != "${rootProject.name}-domain-robstoll-jvm" &&
                        it.name != "${rootProject.name}-core-robstoll-jvm" &&
                        it.name != "${rootProject.name}-spec" &&
                        it.name != "${rootProject.name}-bc-test" &&
                        //TODO remove with 1.0.0
                        it.name != "${rootProject.name}-assertions" &&
                        it.name != "${rootProject.name}-core-api-deprecated" &&
                        it.name != "${rootProject.name}-core-robstoll-deprecated" &&
                        it.name != "${rootProject.name}-domain-api-deprecated" &&
                        it.name != "${rootProject.name}-translations-de_CH-deprecated" &&
                        it.name != "${rootProject.name}-translations-en_UK-deprecated"
                }
        ]

        srcAndResourcesFromJvmProject = { Project project ->
            def nameWithoutSuffix = project.name.substring(project.name.indexOf('-') + 1, project.name.lastIndexOf('-'))
            def jvmProject = prefixedProject(nameWithoutSuffix + '-jvm')
            project.sourceSets {
                main {
                    kotlin { srcDirs += ["${jvmProject.projectDir}/src/main/kotlin"] }
                    resources { srcDirs += ["${jvmProject.projectDir}/src/main/resources"] }
                }
                test {
                    resources { srcDirs += ["${jvmProject.projectDir}/src/test/resources"] }
                }
            }
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "ch.tutteli:tutteli-gradle-project-utils:$gradle.ext.tutteli_plugins_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "ch.tutteli:tutteli-gradle-dokka:$gradle.ext.tutteli_plugins_version"
        classpath "ch.tutteli:tutteli-gradle-kotlin-module-info:$gradle.ext.tutteli_plugins_version"
        classpath "ch.tutteli:tutteli-gradle-kotlin-utils:$gradle.ext.tutteli_plugins_version"
        classpath "ch.tutteli:tutteli-gradle-publish:$gradle.ext.tutteli_plugins_version"
        classpath "ch.tutteli:tutteli-gradle-spek:$gradle.ext.tutteli_plugins_version"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: "ch.tutteli.project.utils"
apply plugin: 'ch.tutteli.kotlin.utils'
kotlinutils.kotlinVersion = kotlin_version

repositories {
    mavenCentral()
}

subprojects {
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/robstoll/tutteli-jars" }
    }
}

def commonProjects = getCommonProjects()
def jsProjects = getJsProjects()
def jvmProjects = getJvmProjects()
def multiplatformProjects = commonProjects + jsProjects + jvmProjects

configureCommonProjects()
configureJsProjects()
configureJvmProjects()
configureAndroidProjects()

configure(subprojects - multiplatformProjects) {
    apply plugin: 'kotlin'
    dependencies {
        compile kotlinStdlib()
    }
}

configure(subprojects - commonProjects - jsProjects) {
    apply plugin: 'ch.tutteli.dokka'
    apply plugin: 'ch.tutteli.kotlin.module.info'
}

def apiProjects = subprojects.findAll { it.name.startsWith("${rootProject.name}-api") }
configure(apiProjects) { apiProject ->
    createTestJarTask(apiProject)
    createTestSourcesJarTask(apiProject)
}

// has to be after the above two blocks which define `it.ext.publish_artifacts`
def bundleSmokeTests = subprojects.findAll { it.name.endsWith('smoke-test') }
configure(subprojects - bundleSmokeTests) { subproject ->
    apply plugin: 'ch.tutteli.publish'
}

configure(jacocoMulti.jacocoProjects + getAndroidProjects()) {
    apply plugin: 'ch.tutteli.spek'

    dependencies {
        testCompile mockito(), excludeKotlin
    }

    afterEvaluate {
        junitjacoco {

            jacoco {
                toolVersion = jacoco_tool_version
            }

            jacocoReport {
                if (project.hasProperty('jacoco_additional')) {
                    project.jacoco_additional.each {
                        sourceDirectories += it.sourceSets.main.allJava.sourceDirectories
                        classDirectories += it.sourceSets.main.output.classesDirs
                    }
                }
                reports {
                    html.enabled = true
                }
            }
        }
    }
}

apply from: 'gradle/scripts/gh-pages.gradle'

configure(bundleSmokeTests) {
    def bundleUnderTest = it.name.substring(0, it.name.indexOf('-smoke-test'))
    def bundle = project(":$bundleUnderTest-jvm")
    description = "Represents a JDK9 smoke test for $bundleUnderTest"

    sourceCompatibility = '9'
    targetCompatibility = '9'

    ext.jacoco_additional = [bundle]

    sourceSets {
        //we are reusing the source from the bundle, so that we do not have to re-invent the spec
        test { kotlin { srcDirs += ["${bundle.projectDir}/src/test/kotlin"] } }
    }

    dependencies {
        //I don't see how to set up compileTestKotlin with --patch-module, so we have put the module-info.java directly in src/test/kotlin instead
        testCompile bundle
        testCompile prefixedProject('verbs-jvm')
    }
}

import java.util.function.Function

def createRegisterJsServicesTask(String projectName, String packageName, Function<String, Boolean> filter) {
    def project = prefixedProject(projectName)
    configure(project) {
        def registerJsServices = project.tasks.create(name: 'generateJsRegisterServices', group: 'build') {
            def jvm = "${getProjectNameWithoutSuffix(project)}-jvm"
            def servicesDir = project.file("../$jvm/src/main/resources/META-INF/services/")
            def services = project.file("$srcKotlin/${packageName.replace('.', '/')}/registerServices.kt")
            inputs.dir servicesDir
            inputs.file services
            outputs.file services

            doLast {
                services.write("""\
                package $packageName
    
                import ch.tutteli.atrium.core.polyfills.registerService
                
                @Suppress("unused" /* here in order that the code is executed when module is loaded */)
                private val register = run {
            """.stripIndent())
                servicesDir.traverse {
                    if (it.isFile() && filter.apply(it.name)) {
                        services.append("\n    registerService<")
                        services.append(it.name)
                        services.append("> { ")
                        services.append(it.withReader { it.readLine() })
                        services.append("() }")
                    }
                }
                services.append("\n}")
            }
        }
        compileKotlin2Js.dependsOn registerJsServices
    }
}


createRegisterJsServicesTask('core-robstoll-js', 'ch.tutteli.atrium.robstoll.core') { true }
createRegisterJsServicesTask('domain-robstoll-js', 'ch.tutteli.atrium.domain.robstoll') {
    it != 'ch.tutteli.atrium.domain.creating.BigDecimalAssertions'
}
createRegisterJsServicesTask('domain-builders-js', 'ch.tutteli.atrium.domain.builders') { true }

def createJsTestTask(subprojectName){
    configure(prefixedProject(subprojectName)) {
        apply plugin: 'com.moowork.node'

        compileTestKotlin2Js.configure {
            kotlinOptions.moduleKind = "commonjs"
            kotlinOptions.sourceMap = true
            kotlinOptions.sourceMapEmbedSources = "always"
        }

        task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
            from compileKotlin2Js.destinationDir

            prefixedProject('verbs-internal-js').afterEvaluate {
                configurations.testRuntimeClasspath.each {
                    from zipTree(it.absolutePath).matching { include '*.js', '*.js.map' }
                }
            }

            into "${buildDir}/node_modules"
        }

        def nodeModulesParentDir = file("$rootProject.projectDir/gradle/")
        node {
            download = true
            npmVersion = '6.4.1'
            workDir = file("$rootProject.projectDir/.gradle/nodejs")
            npmWorkDir = file("$rootProject.projectDir/.gradle/npm")
            nodeModulesDir = nodeModulesParentDir
        }

        task installMocha(type: NpmTask) {
            args = ['install', 'mocha', '--prefer-offline']
        }

        task prepareMocha(dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha])
        task runMocha(type: NodeTask, dependsOn: [prepareMocha, test]) {
            script = file("$nodeModulesParentDir/node_modules/mocha/bin/mocha")
            args = [compileTestKotlin2Js.outputFile]
        }
        check.dependsOn runMocha
    }
}

createJsTestTask('core-api-js')
createJsTestTask('core-robstoll-lib-js')

/*

Release & deploy a commit
--------------------------------
1. change rootProject.version in build.gradle to X.Y.Z
2. update master:
    a) point to the tag
        1) search for old version and replace with new (gradle, maven and section own assertion verb in README.md)
        2) search for `branch=master` and replace it with `tag=vX.Y.Z` (build status and coverage in README.md)
        3) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
        4) search for `latest#/doc` and replace with `X.Y.Z/doc` (README.md and differences.md)
        5) Remove the warning in README.md about taking a sneak peak (copy it, well be added afterwards)
    b) Update README -> Use own Assertion Verbs -> link to atriumVerbs if it changed
    c) commit & push (modified build.gradle, README.md and differences.md)
3. update github pages:
    a) gr ghPages
    b) change version number in atrium-gh-pages/latest/index.html
    c) add new version to atrium-gh-pages/README.md
    d) commit & push changes
4. deploy to bintray:
    a) gr clean publishBintray
    b) Log in to bintray, check and publish new jars
5. create release on github
    a) git tag vX.Y.Z
    b) git push origin vX.Y.Z
    c) Log in to github and create release

Prepare next dev cycle
-----------------------
1. change rootProject.version in build.gradle to X.Y.Z-SNAPSHOT
2. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
   c) search for `X.Y.Z/doc` and replace with `latest#/doc`
4. update README
   a) place the warning about taking a sneak peek back into README
   b) update version in the warning to X.Y.Z and update the link as well
3. commit & push changes

*/
