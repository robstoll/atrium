rootProject.name = 'atrium'

def includeApi(String name) { includeInSubFolder(name, "${rootProject.projectDir}/apis") }

def includeCore(String name) { includeInSubFolder(name, "${rootProject.projectDir}/core") }

def includeTranslation(String name) { includeInSubFolder(name, "${rootProject.projectDir}/translations") }

def includeBundle(String name) { includeInSubFolder(name, "${rootProject.projectDir}/bundles") }

def includeInSubFolder(String name, String folder) {
    include ":$name"
    project(":$name").projectDir = new File("$folder/$name")
}

includeApi "${rootProject.name}-api-cc-de_CH"
includeApi "${rootProject.name}-api-cc-en_UK"
includeApi "${rootProject.name}-api-cc-infix-en_UK"
includeCore "${rootProject.name}-core-api"
includeCore "${rootProject.name}-core-api-late-binding"
includeCore "${rootProject.name}-core-impl-robstoll"
includeCore "${rootProject.name}-core-impl-robstoll-lib"
includeTranslation "${rootProject.name}-translations-de_CH"
includeTranslation "${rootProject.name}-translations-en_UK"
include "${rootProject.name}-assertions"
include "${rootProject.name}-spec"
includeInSubFolder("${rootProject.name}-verbs", "${rootProject.projectDir}/misc")

//convenience modules
includeBundle "${rootProject.name}-cc-en_UK-robstoll"
includeBundle "${rootProject.name}-cc-de_CH-robstoll"
includeBundle "${rootProject.name}-cc-infix-en_UK-robstoll"
