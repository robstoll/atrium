rootProject.name = 'atrium'

buildscript {
    gradle.ext.tutteli_plugins_version = '0.31.2'
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "ch.tutteli:tutteli-gradle-settings:$gradle.ext.tutteli_plugins_version"
    }
}

apply plugin: 'ch.tutteli.settings'

//TODO remove as soon as https://youtrack.jetbrains.com/issue/KT-29069 is fixed
def kotlinJvmJsAndAndroidIfCi = { self, String suffix ->
    if (System.getenv('CI')) {
        self.kotlinJvmJsAndroid(suffix)
    } else {
        self.kotlinJvmJs(suffix)
    }
}
def kotlinJvmAndAndroidIfCi = { self, String suffix ->
    self.folder(suffix, "$suffix-") {
        prefixed('common', 'jvm')
        if (System.getenv('CI')) {
            prefixed('android')
        }
    }
}

def bundleWithExtensionsAndSmokeTest = { self, String apiName ->
    kotlinJvmJsAndAndroidIfCi(self, apiName)

    if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        self.folder(apiName) {
            _ "$apiName-smoke-test"
            folder("extensions"){
                _ "$apiName-smoke-test-jdk8"
                _ "$apiName-smoke-test-kotlin_1_3"
            }
        }
    }
}

def apiWithExtensions = { self, String apiName ->
    kotlinJvmJsAndAndroidIfCi(self, apiName)
    self.folder(apiName, "$apiName-") {
        extensions {
            kotlinJvmAndAndroidIfCi(delegate, 'jdk8')
            kotlinJvmJsAndAndroidIfCi(delegate, 'kotlin_1_3')
        }
    }
}

//noinspection GroovyAssignabilityCheck
include {

    bundles {
        bundleWithExtensionsAndSmokeTest(delegate, 'fluent-en_GB')
    }

    apis('api-') {
        apiWithExtensions(delegate, 'fluent-en_GB')
        //TODO re-activate with 1.0.0
//        apiWithExtensions(delegate, 'infix-en_GB')
    }

    domain('domain-') {
        kotlinJvmJsAndAndroidIfCi(delegate, 'api')
        kotlinJvmJsAndAndroidIfCi(delegate, 'builders')
        kotlinJvmJsAndAndroidIfCi(delegate, 'robstoll')
        kotlinJvmJsAndAndroidIfCi(delegate, 'robstoll-lib')

        api('api-') {
            extensions {
                kotlinJvmJsAndAndroidIfCi(delegate, 'kotlin_1_3')
            }
        }

        builders('builders-') {
            extensions {
                kotlinJvmJsAndAndroidIfCi(delegate, 'kotlin_1_3')
            }
        }

        robstoll('robstoll-') {
            extensions {
                kotlinJvmJsAndAndroidIfCi(delegate, 'kotlin_1_3')
            }
        }

        folder('robstoll-lib', 'robstoll-lib-') {
            extensions {
                kotlinJvmJsAndAndroidIfCi(delegate, 'kotlin_1_3')
            }
        }
    }

    core('core-') {
        kotlinJvmJsAndAndroidIfCi(delegate, 'api')
        kotlinJvmJsAndAndroidIfCi(delegate, 'robstoll')
        kotlinJvmJsAndAndroidIfCi(delegate, 'robstoll-lib')
    }

    translations('translations-') {
        kotlinJvmJsAndAndroidIfCi(delegate, 'de_CH')
        kotlinJvmJsAndAndroidIfCi(delegate, 'en_GB')
    }

    misc {
        _ 'spec'
        kotlinJvmJsAndAndroidIfCi(delegate, 'specs')
        kotlinJvmJsAndAndroidIfCi(delegate, 'verbs')
        kotlinJvmJsAndAndroidIfCi(delegate, 'verbs-internal')

        tools {
            _ 'bc-test'
        }
    }

    samples {
        js {
            project('mocha')
            project('jasmine')
        }
        project('readme-examples')
    }


    //TODO remove all below with 1.0.0
    bundles {
        _ 'cc-en_UK-robstoll'
        _ 'cc-infix-en_UK-robstoll'
        kotlinJvmJsAndAndroidIfCi(delegate, 'cc-de_CH-robstoll')
        kotlinJvmJsAndAndroidIfCi(delegate, 'cc-en_GB-robstoll')
        kotlinJvmJsAndAndroidIfCi(delegate, 'cc-infix-en_GB-robstoll')

        if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
            folder('cc-en_GB-robstoll') {
                _ 'cc-en_GB-robstoll-smoke-test'
            }
            folder('cc-infix-en_GB-robstoll') {
                _ 'cc-infix-en_GB-robstoll-smoke-test'
            }
        }
    }

    folder('apis', 'api-') {
        _ 'cc-en_UK'
        _ 'cc-infix-en_UK'
        kotlinJvmJsAndAndroidIfCi(delegate, 'cc-de_CH')
        kotlinJvmJsAndAndroidIfCi(delegate, 'cc-en_GB')
        kotlinJvmJsAndAndroidIfCi(delegate, 'cc-infix-en_GB')
    }
    domain('domain-') {
        _ 'api-deprecated'
    }
    core('core-') {
        _ 'api-deprecated'
        _ 'robstoll-deprecated'
    }
    translations('translations-') {
        _ 'de_CH-deprecated'
        _ 'en_UK-deprecated'
    }
    misc {
        _ 'assertions'
    }
}
