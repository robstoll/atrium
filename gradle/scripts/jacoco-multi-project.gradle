apply plugin: 'kotlin'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "$jacoco_tool_version"
}

task jacocoMerge(type: JacocoMerge) {
    jacocoMulti.jacocoProjects.each {
        executionData "${it.buildDir}/jacoco/junitPlatformTest.exec"
    }
}

task CodeCoverageReport(type: JacocoReport) {
    dependsOn jacocoMulti.sourceProjects.build
    dependsOn jacocoMerge
    jacocoMerge.mustRunAfter jacocoMulti.sourceProjects.build

    executionData jacocoMerge.destinationFile

    sourceDirectories = files()
    classDirectories = files()

    jacocoMulti.sourceProjects.each {
        sourceDirectories += it.sourceSets.main.allJava.sourceDirectories
        classDirectories += files(it.sourceSets.main.output.classesDirs)
    }
    doFirst {
        println "going to generate merged jacoco report based on:"
        println "  - source projects: ${jacocoMulti.sourceProjects*.name}"
        println "  - jacoco projects: ${jacocoMulti.jacocoProjects*.name}"
        println "========================================="
    }

    reports {
        csv.enabled false
        html.enabled = true
        html.destination file("${buildDir}/jacoco/")
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}
