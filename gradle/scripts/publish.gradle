import com.jfrog.bintray.gradle.BintrayPlugin

group = publish_groupId
version = publish_version
description = publish_description

buildscript {
    ext{
        publish_license_name = findProperty('publish_license_name') ?: 'EUPL-1.2'
        publish_license_description =  findProperty('publish_license_description') ?: 'European Union Public Licence, Version 1.2'
        publish_license_url = findProperty('publish_license_url') ?: 'https://joinup.ec.europa.eu/collection/eupl/eupl-text-11-12'
        publish_license_distribution = findProperty('publish_license_distribution') ?: 'repo'
        publish_dev_id = findProperty('publish_dev_id') ?: 'robstoll'
        publish_dev_name = findProperty('publish_dev_name') ?: 'Robert Stoll'
        publish_dev_email = findProperty('publish_dev_email') ?: 'rstoll@tutteli.ch'

        publish_repo = findProperty('publish_repo') ?: 'tutteli-jars'
        publish_groupId = findProperty('publish_groupId') ?: 'ch.tutteli'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
    }
}

apply plugin: BintrayPlugin
apply plugin: 'maven-publish'


def pomConfig = {
    licenses {
        license {
            name "$publish_license_description"
            url "$publish_license_url"
            distribution "$publish_license_distribution"
        }
    }
    developers {
        developer {
            id "$publish_dev_id"
            name "$publish_dev_name"
            email "$publish_dev_email"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId "$publish_groupId"
            artifactId "$publish_artifactId"
            version "$publish_version"

            pom.withXml {
                def root = asNode()
                root.appendNode('description', "$publish_description")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = findProperty('bintrayApiKey') ?: System.getenv('BINGTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = "$publish_repo"
        name = "$publish_artifactId"
        licenses = ["$publish_license_name"]
        vcsUrl = "$publish_vcsUrl"
        version {
            name = "$publish_version"
            released  = new Date()
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase') ?: System.getenv('GPG_PASSPHRASE')
            }
        }
    }
}
