apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

def ghPagesPathWithoutVersion = "../${project.name}-gh-pages"
def ghPagesPath = "$ghPagesPathWithoutVersion/$ghPages_version"

task copyIndex(type: Copy) {
    from "$ghPagesPathWithoutVersion/index.version.md"
    into ghPagesPath
    rename('index.version.md', 'index.md')
    expand(version: ghPages_version)
    filteringCharset = 'UTF-8'
}

task copyCSS(type: Copy) {
    from "$ghPagesPathWithoutVersion/style.css"
    into  ghPagesPath
}

task cleanGhPages {
    doFirst { delete ghPagesPath }
}

task ghPages {
    dependsOn cleanGhPages
    dependsOn dokka
    dokka.mustRunAfter cleanGhPages
    finalizedBy copyCSS
    finalizedBy copyIndex
}

dokka {
    moduleName = 'doc'
    outputFormat = 'html'
    outputDirectory = "$ghPagesPathWithoutVersion/$ghPages_version"

    includes = ['misc/kdoc/packages.md']
    jdkVersion = 6

    doFirst {
        println "going to generate kdoc including the projects: ${docProjects*.name}"
        println "====================="
    }
    docProjects.each { subProject ->
        linkMapping {
            dir = "${subProject.projectDir}/$srcKotlin"
            url = "$github_url/${dokka_sourceMapping}/${subProject.projectDir}/$srcKotlin"
            suffix = '#L'
        }
    }
    sourceDirs = docProjects.collect { subProject -> file("${subProject.projectDir}/$srcKotlin") }
}
